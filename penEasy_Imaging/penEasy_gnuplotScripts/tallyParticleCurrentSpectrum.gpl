# gnuplot script file for penEasy
# Last update:
#   2008-04-19 by JS

# Factor from keV to eV:
f = 1.0e3

####################
# Particle spectra #
####################

reset
set style line 1 linetype -1 linewidth 1 pointtype 7 pointsize 0.5

set title "Positron Current Spectrum"
set xlabel "energy[E] (keV)"
set ylabel "p(E) (1/keV per history)"
plot "tallyParticleCurrentSpectrum-positron.dat" using ($2/f):($3*f):($4*f) notitle with yerrorbars linestyle 1, \
     "tallyParticleCurrentSpectrum-positron.dat" using ($1/f):($3*f) notitle with steps linestyle 1
pause -1 "Press OK"

set title "Photon Current Spectrum"
set xlabel "energy[E] (keV)"
set ylabel "p(E) (1/keV per history)"
plot "tallyParticleCurrentSpectrum-photon.dat" using ($2/f):($3*f):($4*f) notitle with yerrorbars linestyle 1, \
     "tallyParticleCurrentSpectrum-photon.dat" using ($1/f):($3*f) notitle with steps linestyle 1
pause -1 "Press OK"

set title "Electron Current Spectrum"
set xlabel "energy[E] (keV)"
set ylabel "p(E) (1/keV per history)"
plot "tallyParticleCurrentSpectrum-electron.dat" using ($2/f):($3*f):($4*f) notitle with yerrorbars linestyle 1, \
     "tallyParticleCurrentSpectrum-electron.dat" using ($1/f):($3*f) notitle with steps linestyle 1
pause -1 "Press OK"


##################
# Energy current #
##################

reset
set style line 1 linetype -1 linewidth 1 pointtype 7 pointsize 1

set title "Total Energy Current"
set xtics ("electrons" -1, "photons" 0, "positrons" +1, "total" +3)
set ylabel "Energy (keV per history)"
set boxwidth 0.5 relative
set style fill solid 0.5
plot [-2:4] [0:] "tallyParticleCurrentSpectrum-totalE.dat" using 1:($2/f) notitle with boxes linestyle 1, \
                 "tallyParticleCurrentSpectrum-totalE.dat" using 1:($2/f):($3/f) notitle with yerrorbars linestyle 1
pause -1 "Press OK"

