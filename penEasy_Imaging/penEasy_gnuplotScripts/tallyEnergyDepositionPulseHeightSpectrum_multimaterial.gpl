# gnuplot script file for penEasy
# Last update:
#   2008-04-19 by JS

# Factor from eV to keV:
f = 1.0e-3

reset
set style line 1 linetype -1 linewidth 1 pointtype 7 pointsize 1

set title "Total Energy Deposition"
set xrange [0:11]
# set xtics ("" 0)
set ylabel "Energy (keV per history)"
set xlabel "Material number"

plot "tallyEnergyDeposition.dat" index 0 using 1:($2*f):($3*f) notitle with yerrorlines linestyle 1

pause -1 "Press OK"

reset
set style line 1 linetype -1 linewidth 1 pointtype 7 pointsize 0.5

set title "Pulse Height Spectrum"
set xlabel "energy[E] (keV)"
set ylabel "p(E) (1/keV per history)"
plot "tallyPulseHeightSpectrum.dat" using ($2*f):($3/f):($4/f) notitle with yerrorbars linestyle 1, \
     "tallyPulseHeightSpectrum.dat" using ($1*f):($3/f) notitle with steps linestyle 1
pause -1 "Press OK"

